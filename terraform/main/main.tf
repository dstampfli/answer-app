data "terraform_remote_state" "main" {
  backend = "gcs"
  config = {
    bucket                      = "terraform-state-${var.project_id}"
    impersonate_service_account = var.terraform_service_account
    prefix                      = "main"
  }
  workspace = terraform.workspace
}

locals {
  # Load the configuration file.
  config = yamldecode(file("../../src/answer_app/config.yaml"))

  # Get optional redundant Cloud Run backend deployment regions or fallback to an empty list.
  additional_regions = coalesce(local.config.additional_regions, [])

  # Use the load balancer domain name from the configuration file if it is set.
  # Otherwise, get the domain name from the load balancer module output.
  lb_domain = coalesce(local.config.loadbalancer_domain, try(module.loadbalancer[0].lb_domain, null))

  # Read the Docker image name from an input variable.
  # Otherwise, use the existing image and get the name from the remote state output.
  docker_image = coalesce(var.docker_image, try(data.terraform_remote_state.main.outputs.docker_image, null))
}

module "loadbalancer" {
  source          = "../modules/loadbalancer"
  count           = local.config.create_loadbalancer ? 1 : 0
  project_id      = var.project_id
  lb_domain       = local.config.loadbalancer_domain
  default_service = module.answer_app.cloudrun_client_backend_service_id
  backend_services = [
    {
      paths   = ["/${module.answer_app.service_name}/*"]
      service = module.answer_app.cloudrun_backend_service_id
    },
    {
      paths   = ["/${module.answer_app.client_service_name}", "/${module.answer_app.client_service_name}/*"]
      service = module.answer_app.cloudrun_client_backend_service_id
    },
  ]
}

module "answer_app" {
  source             = "../modules/answer-app"
  project_id         = var.project_id
  region             = var.region
  additional_regions = local.additional_regions
  app_name           = local.config.app_name
  lb_domain          = local.lb_domain
  docker_image       = local.docker_image
  location           = local.config.location
  dataset_id         = local.config.dataset_id
  table_id           = local.config.table_id
  feedback_table_id  = local.config.feedback_table_id

  data_stores = {
    "${local.config.app_name}-default" = {
      data_store_id = local.config.data_store_id
    }
  }
  search_engine = {
    search_engine_id = local.config.search_engine_id
    company_name     = local.config.customer_name
  }
}
